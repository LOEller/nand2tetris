class Snake {

   field SnakeSegment head;
   field SnakeSegment tail;
   field int direction;

   constructor Snake new() {
      var SnakeSegment middle; 

      // start with a three segment snake moving down
      let direction = 2;
      let head = SnakeSegment.new(2, 4);
      let middle = SnakeSegment.new(2, 3);
      let tail = SnakeSegment.new(2, 2);

      // set up pointers in doubly linked list
      do head.setPrev(middle);
      do middle.setNext(head);
      do middle.setPrev(tail);
      do tail.setNext(middle);
      return this;
   }

   method void dispose() {
      do head.dispose(); // recursively deletes all snake segments 
      do Memory.deAlloc(this);
      return;
   }

   method void setDirection(int Adirection) {
     let direction = Adirection;
     return;
   }

   method int getNewX() {
     if (direction = 1) {
       return head.getX();
     }
     if (direction = 2) {
       return head.getX();
     }
     if (direction = 3) {
       return head.getX() - 1;
     }
     if (direction = 4) {
       return head.getX() + 1;
     }
     return 0;
   }

   method int getNewY() {
     if (direction = 1) {
       return head.getY() - 1;
     }
     if (direction = 2) {
       return head.getY() + 1;
     }
     if (direction = 3) {
       return head.getY();
     }
     if (direction = 4) {
       return head.getY();
     }
     return 0;
   }

   method void moveSnake() {
     var SnakeSegment temp;
     var int newX, newY;

     let newX = getNewX();
     let newY = getNewY();
     
     // append a new head to the snake in the direction the 
     // snake is currently moving and draw

     let temp = SnakeSegment.new(newX, newY); // temp becomes the new head
     do temp.setPrev(head);
     if ((newX < 51) & (newY < 24)) {
       if ((newX > -1) & (newY > -1)) {
         do temp.draw();
       }
     }
     do head.setNext(temp);
     let head = temp;

     // erase the tail segment of the snake then delete that node
     let temp = tail;
     let tail = tail.getNext(); // reassign tail pointer
     do temp.erase();
     do Memory.deAlloc(temp); // delete the previous tail
     do tail.setPrev(null);
    
     return;
   }

   // adds a segment to this snake
   method void addSegment() {
      var SnakeSegment temp;
      let temp = SnakeSegment.new(getNewX(), getNewY());

      do temp.setPrev(head);
      do head.setNext(temp);
      let head = temp;
      do head.draw();
      return;
   }

   method boolean detectSelfCollision() {
     // determines whether the head of the snake collides with any of its
     // own segments
     var SnakeSegment current;
     let current = head.getPrev();

     while (~(current = null)) {
        if ((current.getX() = head.getX()) & (current.getY() = head.getY())) {
            return true;
        }
        let current = current.getPrev();
     }
     return false;
   }

   method int getX() {
     return head.getX();
   }

   method int getY() {
     return head.getY();
   }
}
